---
# Core Classification
protocol: Frax Finance
chain: everychain
category: uncategorized
vulnerability_type: unknown

# Attack Vector Details
attack_type: unknown
affected_component: smart_contract

# Source Information
source: solodit
solodit_id: 17911
audit_firm: TrailOfBits
contest_link: https://github.com/trailofbits/publications/blob/master/reviews/FraxQ22022.pdf
source_link: https://github.com/trailofbits/publications/blob/master/reviews/FraxQ22022.pdf
github_link: none

# Impact Classification
severity: high
impact: security_vulnerability
exploitability: 0.00
financial_impact: high

# Scoring
quality_score: 0
rarity_score: 0

# Context Tags
tags:

protocol_categories:
  - liquid_staking
  - dexes
  - cdp
  - yield
  - services

# Audit Details
report_date: unknown
finders_count: 2
finders:
  - Gustavo Grieco
  - Michael Colburn
---

## Vulnerability Title

Unsafe integer conversions in FPIControllerPool

### Overview


This bug report is about a data validation issue in the FPIControllerPool contract, which allows certain users to borrow or repay FRAX within certain limits (e.g., the borrowing cap). The issue is that explicit integer conversions can be used to bypass these restrictions. For example, if frax_amount is set to a very large unsigned integer, it could be cast to a negative number, allowing malicious users to exploit the code and bypass the limits. This same issue also affects the implementation of price_info.

The report provides two recommendations to address this issue. The first is to add checks to the relevant functions to validate the results of explicit integer conversions to ensure they are within the expected range. The second is to use extensive smart contract fuzzing to test that system invariants cannot be broken.

### Original Finding Content

## Difficulty: Low

## Type: Data Validation

## Description

Explicit integer conversions can be used to bypass certain restrictions (e.g., the borrowing cap) in the `FPIControllerPool` contract. The `FPIControllerPool` contract allows certain users to either borrow or repay FRAX within certain limits (e.g., the borrowing cap):

```solidity
// Lend the FRAX collateral to an AMO
function giveFRAXToAMO(address destination_amo, uint256 frax_amount) external onlyByOwnGov validAMO(destination_amo) {
    int256 frax_amount_i256 = int256(frax_amount);
    // Update the balances first
    require((frax_borrowed_sum + frax_amount_i256) <= frax_borrow_cap, "Borrow cap");
    frax_borrowed_balances[destination_amo] += frax_amount_i256;
    frax_borrowed_sum += frax_amount_i256;
    // Give the FRAX to the AMO
    TransferHelper.safeTransfer(address(FRAX), destination_amo, frax_amount);
}

// AMO gives back FRAX. Needed for proper accounting
function receiveFRAXFromAMO(uint256 frax_amount) external validAMO(msg.sender) {
    int256 frax_amt_i256 = int256(frax_amount);
    // Give back first
    TransferHelper.safeTransferFrom(address(FRAX), msg.sender, address(this), frax_amount);
    // Then update the balances
    frax_borrowed_balances[msg.sender] -= frax_amt_i256;
    frax_borrowed_sum -= frax_amt_i256;
}
```

*Figure 4.1: The `giveFRAXToAMO` function in `FPIControllerPool.sol`*

However, these functions explicitly convert these variables from `uint256` to `int256`; these conversions will never revert and can produce unexpected results. For instance, if `frax_amount` is set to a very large unsigned integer, it could be cast to a negative number. Malicious users could exploit this fact by adjusting the variables to integers that will bypass the limits imposed by the code after they are cast.

The same issue affects the implementation of `price_info`:

```solidity
// Get additional info about the peg status
function price_info() public view returns (
    int256 collat_imbalance,
    uint256 cpi_peg_price,
    uint256 fpi_price,
    uint256 price_diff_frac_abs
) {
    fpi_price = getFPIPriceE18();
    cpi_peg_price = cpiTracker.currPegPrice();
    uint256 fpi_supply = FPI_TKN.totalSupply();
    
    if (fpi_price > cpi_peg_price) {
        collat_imbalance = int256(((fpi_price - cpi_peg_price) * fpi_supply) / PRICE_PRECISION);
        price_diff_frac_abs = ((fpi_price - cpi_peg_price) * PEG_BAND_PRECISION) / fpi_price;
    } else {
        collat_imbalance = -1 * int256(((cpi_peg_price - fpi_price) * fpi_supply) / PRICE_PRECISION);
        price_diff_frac_abs = ((cpi_peg_price - fpi_price) * PEG_BAND_PRECISION) / fpi_price;
    }
}
```

*Figure 4.2: The `price_info` function in `FPIControllerPool.sol`*

## Exploit Scenario

Eve submits a governance proposal that can increase the amount of FRAX that can be borrowed. The voters approve the proposal because they believe that the borrowing cap will stop Eve from changing it to a larger value.

## Recommendations

- **Short term:** Add checks to the relevant functions to validate the results of explicit integer conversions to ensure that they are within the expected range.
- **Long term:** Use extensive smart contract fuzzing to test that system invariants cannot be broken.

### Metadata

| Field | Value |
|-------|-------|
| Impact | HIGH |
| Quality Score | 0/5 |
| Rarity Score | 0/5 |
| Audit Firm | TrailOfBits |
| Protocol | Frax Finance |
| Report Date | N/A |
| Finders | Gustavo Grieco, Michael Colburn |

### Source Links

- **Source**: https://github.com/trailofbits/publications/blob/master/reviews/FraxQ22022.pdf
- **GitHub**: N/A
- **Contest**: https://github.com/trailofbits/publications/blob/master/reviews/FraxQ22022.pdf

### Keywords for Search

`vulnerability`

