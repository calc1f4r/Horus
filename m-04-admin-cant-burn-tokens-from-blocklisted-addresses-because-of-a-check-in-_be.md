---
# Core Classification
protocol: Ondo Finance
chain: everychain
category: uncategorized
vulnerability_type: unknown

# Attack Vector Details
attack_type: unknown
affected_component: smart_contract

# Source Information
source: solodit
solodit_id: 27016
audit_firm: Code4rena
contest_link: https://code4rena.com/reports/2023-09-ondo
source_link: https://code4rena.com/reports/2023-09-ondo
github_link: https://github.com/code-423n4/2023-09-ondo-findings/issues/136

# Impact Classification
severity: medium
impact: security_vulnerability
exploitability: 0.00
financial_impact: medium

# Scoring
quality_score: 0
rarity_score: 0

# Context Tags
tags:

protocol_categories:
  - leveraged_farming
  - rwa
  - services
  - cdp
  - liquid_staking

# Audit Details
report_date: unknown
finders_count: 7
finders:
  - 0xStalin
  - Arz
  - Inspecktor
  - 0xAsen
  - merlin
---

## Vulnerability Title

[M-04] Admin can't burn tokens from blocklisted addresses because of a check in `_beforeTokenTransfer`

### Overview


The bug report is about a function called `burn` which is made so the admin can burn rUSDY tokens from any account. However, the admin can't burn tokens if the account from which they're trying to burn tokens is blocklisted/sanctioned/not on the allow-list. The bug is caused by the code of `_beforeTokenTransfer` function, which checks constraints when `transferFrom` is called to facilitate a transfer between two parties that are not `from` or `to`. This means that if the `from` address is not `address(0)`, it will check if it is blocked, sanctioned, or not on the allow-list. If it is, the transaction will revert and the tokens won't be burned.

The recommended mitigation steps are to organize the logic of the function better. For example, the 2nd `if` statement can be changed to `if (from != address(0) && to != address(0))`. This way, it will not enter the `if` statement when burning tokens, and the tokens can be burned from blocked accounts.

The assessed type of the bug is Invalid Validation. It was initially disputed by tom2o17, who suggested that the guardian can batch execute transactions to bypass the issue. However, kirk-baird, the judge, downgraded the issue to medium severity as there are some theoretical workarounds to this problem. tom2o17 then clarified that they are using a gnosis safe contract as the guardian, which resolves the issue. The issue was then acknowledged by ali2251.

### Original Finding Content


The function `burn` is made so the admin can burn rUSDY tokens from **any account** (this is stated in the comments). However, the admin can't burn tokens if the account from which they're trying to burn tokens is blocklisted/sanctioned/not on the allow-list.

### Proof of Concept

Let's check the `burn` function which calls the internal `_burnShares` function:

```javascript
function burn(
    address _account,
    uint256 _amount
  ) external onlyRole(BURNER_ROLE) {
    uint256 sharesAmount = getSharesByRUSDY(_amount);

    _burnShares(_account, sharesAmount);

    usdy.transfer(msg.sender, sharesAmount / BPS_DENOMINATOR);

    emit TokensBurnt(_account, _amount);
  }

  function _burnShares(
    address _account,
    uint256 _sharesAmount
  ) internal whenNotPaused returns (uint256) {
    require(_account != address(0), "BURN_FROM_THE_ZERO_ADDRESS");

    _beforeTokenTransfer(_account, address(0), _sharesAmount); <--

    uint256 accountShares = shares[_account];
    require(_sharesAmount <= accountShares, "BURN_AMOUNT_EXCEEDS_BALANCE");

    uint256 preRebaseTokenAmount = getRUSDYByShares(_sharesAmount);

    totalShares -= _sharesAmount;

    shares[_account] = accountShares - _sharesAmount;

    uint256 postRebaseTokenAmount = getRUSDYByShares(_sharesAmount);

    return totalShares;
```

We can see that it calls `_beforeTokenTransfer(_account, address(0), _sharesAmount)`.

Here is the code of `_beforeTokenTransfer`:

```javascript
function _beforeTokenTransfer(
    address from,
    address to,
    uint256
  ) internal view {
    // Check constraints when `transferFrom` is called to facilitate
    // a transfer between two parties that are not `from` or `to`.
    if (from != msg.sender && to != msg.sender) {
      require(!_isBlocked(msg.sender), "rUSDY: 'sender' address blocked");
      require(!_isSanctioned(msg.sender), "rUSDY: 'sender' address sanctioned");
      require(
        _isAllowed(msg.sender),
        "rUSDY: 'sender' address not on allowlist"
      );
    }

    if (from != address(0)) { <--
      // If not minting
      require(!_isBlocked(from), "rUSDY: 'from' address blocked");
      require(!_isSanctioned(from), "rUSDY: 'from' address sanctioned");
      require(_isAllowed(from), "rUSDY: 'from' address not on allowlist");
    }

    if (to != address(0)) {
      // If not burning
      require(!_isBlocked(to), "rUSDY: 'to' address blocked");
      require(!_isSanctioned(to), "rUSDY: 'to' address sanctioned");
      require(_isAllowed(to), "rUSDY: 'to' address not on allowlist");
    }
  }
```

In our case, the `form` would be the account from which we're burning tokens, so it'll enter in the 2nd if: `if (from != address(0))`. But given that the account is blocked/sanctioned/not on the allow-list, the transaction will revert and the tokens won't be burned.

Given that there are separate roles for burning and managing the block/sanctions/allowed lists (`BURNER_ROLE` and `LIST_CONFIGURER_ROLE`), it is very possible that such a scenario would occur.

In that case, the Burner would have to ask the List Configurer to update the lists, so the Burner can burn the tokens, and then the List Configurer should update the lists again. However, in that case, you're risking that the blocked user manages to transfer their funds while performing these operations.

### Recommended Mitigation Steps

Organize the logic of the function better. For example, you can make the 2nd if to be:
`if (from != address(0) && to != address(0))`. That way, we'll not enter the `if` when burning tokens, and we'll be able to burn tokens from blocked accounts.

### Assessed type

Invalid Validation

**[tom2o17 (Ondo) disputed and commented via duplicate issue #120](https://github.com/code-423n4/2023-09-ondo-findings/issues/120#issuecomment-1714220911):**
> Can I not assume that the guardian can batch execute transactions?
Given that the guardian will also have the ability to add/remove from blocklist, can I not assume that the guardian can batch:
>
> ```
> blocklist.removeFromBlocklist()
> rUSDY.burn()
> blocklist.addToBlocklist()
> ```
>
> IFL this is not an issue considering the guardian address can execute any peripheral txns in an atomic fashion. 

**[kirk-baird (judge) commented via duplicate issue #120](https://github.com/code-423n4/2023-09-ondo-findings/issues/120#issuecomment-1725127719):**
> This is an interesting edge case. While it may be possible for guardian to bypass this issue, if it is a smart contract that can batch transactions, I see this as a potential issue.
> 
> Going to downgrade [issue #120](https://github.com/code-423n4/2023-09-ondo-findings/issues/120) to medium severity, as there are some theoretical workarounds to this problem.

**[tom2o17 (Ondo) commented via duplicate issue #120](https://github.com/code-423n4/2023-09-ondo-findings/issues/120#issuecomment-1736164013):**
> Not to impact judging,
>
> But to your point @kirk-baird, we are using a gnosis safe contract as the guardian, and plan to do a similar setup for the majority of tokens going forward. Perhaps we should have made note of that in the `ReadME.md`.

**[kirk-baird (judge) commented via duplicate issue #120](https://github.com/code-423n4/2023-09-ondo-findings/issues/120#issuecomment-1736530397):**
> @tom2o17 - Okay thanks for clarifying - that does resolve the issue. Though, for the judging the wardens weren't aware of this so I'll consider it a valid issue.

**[ali2251 (Ondo) acknowledged](https://github.com/code-423n4/2023-09-ondo-findings/issues/136#issuecomment-1738095660)**

***



### Metadata

| Field | Value |
|-------|-------|
| Impact | MEDIUM |
| Quality Score | 0/5 |
| Rarity Score | 0/5 |
| Audit Firm | Code4rena |
| Protocol | Ondo Finance |
| Report Date | N/A |
| Finders | 0xStalin, Arz, Inspecktor, 0xAsen, merlin, Delvir0, BenRai |

### Source Links

- **Source**: https://code4rena.com/reports/2023-09-ondo
- **GitHub**: https://github.com/code-423n4/2023-09-ondo-findings/issues/136
- **Contest**: https://code4rena.com/reports/2023-09-ondo

### Keywords for Search

`vulnerability`

