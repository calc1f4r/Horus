---
# Core Classification
protocol: The Standard Auto Redemption
chain: everychain
category: uncategorized
vulnerability_type: unknown

# Attack Vector Details
attack_type: unknown
affected_component: smart_contract

# Source Information
source: solodit
solodit_id: 45053
audit_firm: Cyfrin
contest_link: none
source_link: https://github.com/solodit/solodit_content/blob/main/reports/Cyfrin/2024-12-18-cyfrin-the-standard-auto-redemption-v2.0.md
github_link: none

# Impact Classification
severity: high
impact: security_vulnerability
exploitability: 0.00
financial_impact: high

# Scoring
quality_score: 0
rarity_score: 0

# Context Tags
tags:

# Audit Details
report_date: unknown
finders_count: 1
finders:
  - Giovanni Di Siena
---

## Vulnerability Title

Hypervisor collateral redemption can cause vaults to become undercollateralized due to slippage

### Overview


The report describes a bug in the redemption process for Hypervisor collateral. When redeeming, the `SmartVaultV4::autoRedemption` function calls `SmartVaultYieldManager::quickWithdraw` which withdraws the collateral without applying the additional protocol withdrawal fee. After redemption, the excess collateral is redeposited to the Hypervisor through `SmartVaultV4::redeposit`, which also does not apply the fee. This can result in undercollateralized vaults if an attacker takes advantage of the lack of slippage handling. To fix this, additional validation should be added to prevent vaults from becoming undercollateralized. The bug has been fixed by The Standard DAO, but Cyfrin suggests handling the case where vault debt is fully repaid to avoid division by zero. The Standard DAO has addressed this issue as well.

### Original Finding Content

**Description:** When redeeming Hypervisor collateral, `SmartVaultV4::autoRedemption` performs a call to `SmartVaultYieldManager::quickWithdraw`:

```solidity
if (_hypervisor != address(0)) {
    address _yieldManager = ISmartVaultManager(manager).yieldManager();
    IERC20(_hypervisor).safeIncreaseAllowance(_yieldManager, getAssetBalance(_hypervisor));
    _withdrawn = ISmartVaultYieldManager(_yieldManager).quickWithdraw(_hypervisor, _collateralToken);
    IERC20(_hypervisor).forceApprove(_yieldManager, 0);
}
```

This invokes `SmartVaultYieldManager::_withdrawOtherDeposit` to withdraw the underlying collateral without applying the additional protocol withdrawal fee:

```solidity
function quickWithdraw(address _hypervisor, address _token) external returns (uint256 _withdrawn) {
    IERC20(_hypervisor).safeTransferFrom(msg.sender, address(this), IERC20(_hypervisor).balanceOf(msg.sender));
    _withdrawOtherDeposit(_hypervisor, _token);
    uint256 _withdrawn = _thisBalanceOf(_token);
    IERC20(_token).safeTransfer(msg.sender, _withdrawn);
}
```

After auto redemption is complete, the excess underlying collateral is redeposited to the Hypervisor through `SmartVaultV4::redeposit`:

```solidity
function redeposit(uint256 _withdrawn, uint256 _collateralBalance, address _hypervisor, address _collateralToken)
    private
{
    uint256 _redeposit = _withdrawn > _collateralBalance ? _collateralBalance : _withdrawn;
    address _yieldManager = ISmartVaultManager(manager).yieldManager();
    IERC20(_collateralToken).safeIncreaseAllowance(_yieldManager, _redeposit);
    ISmartVaultYieldManager(_yieldManager).quickDeposit(_hypervisor, _collateralToken, _redeposit);
    IERC20(_collateralToken).forceApprove(_yieldManager, 0);
}
```

This performs a call to `SmartVaultYieldManager::quickDeposit` which similarly invokes `SmartVaultYieldManager::_otherDeposit_` to deposit the underlying collateral without applying the additional protocol deposit fee:

```solidity
function quickDeposit(address _hypervisor, address _collateralToken, uint256 _deposit) external {
    IERC20(_collateralToken).safeTransferFrom(msg.sender, address(this), _deposit);
    HypervisorData memory _hypervisorData = hypervisorData[_collateralToken];
    _otherDeposit(_collateralToken, _hypervisorData);
}
```

The issue with this logic is that the `SmartVaultYieldManager` functions assume slippage is sufficiently handled by calling function:

```solidity
// within _withdrawOtherDeposit()
IHypervisor(_hypervisor).withdraw(
    _thisBalanceOf(_hypervisor), address(this), address(this), [uint256(0), uint256(0), uint256(0), uint256(0)]
);

// within _swapToSingleAsset(), called from within _withdrawOtherDeposit()
// similar is present within _buy() and _sell() called from within _otherDeposit() -> _swapToRatio()
ISwapRouter(uniswapRouter).exactInputSingle(
    ISwapRouter.ExactInputSingleParams({
        tokenIn: _unwantedToken,
        tokenOut: _wantedToken,
        fee: _fee,
        recipient: address(this),
        deadline: block.timestamp + 60,
        amountIn: _balance,
        amountOutMinimum: 0,
        sqrtPriceLimitX96: 0
    })
);

// within _deposit(), called from within _otherDeposit()
IUniProxy(uniProxy).deposit(
    _thisBalanceOf(_token0),
    _thisBalanceOf(_token1),
    msg.sender,
    _hypervisor,
    [uint256(0), uint256(0), uint256(0), uint256(0)]
);
```

The current logic assumes vaults cannot become undercollateralized as a result of auto redemption and performs no such validation; however, an attacker can cause a vault to become undercollateralized by taking advantage of the lack of slippage handling.

A collateralization check should be performed immediately after the execution of redeposit logic due to empty slippage parameters.

**Impact:** Auto redemption of Hypervisor collateral can result in undercollateralized vaults.

**Recommended Mitigation:** Add additional validation to prevent vaults from becoming undercollateralized as a result of auto redemption.

**The Standard DAO:** Fixed by commit [b71beb0](https://github.com/the-standard/smart-vault/commit/b71beb03aef660f5f7cedcde88167e816111f283).

**Cyfrin:** This prevents the new `SmartVaultV4` with Hypervisor deposits from becoming undercollateralized due to auto redemption, however this does not prevent a significant collateral drawdown to just above the liquidation threshold. As an aside, we should probably also validate that auto redemption is not trying to swap collateral for a vault that is already undercollateralized but not liquidated – prefer a significant drawdown check here instead.

**The Standard DAO:** Fixed by commits [b92c98c](https://github.com/the-standard/smart-vault/commit/b92c98c4217aa566c5e2007f8fdcb9804e200ec9), [cd6bd7c](https://github.com/the-standard/smart-vault/commit/cd6bd7c612ce1476b9aa0f4b01593be0d8b32e82), and [132a013](https://github.com/the-standard/smart-vault/commit/132a0138e3e586493dcd25bfb1b56e1c761f95c2).

**Cyfrin:** The collateral percentage runtime invariant check has been added; however, the case where vault debt is fully repaid needs to be handled explicitly to avoid division by zero in `SmartVaultV4::calculateCollaralPercentage`.

**The Standard DAO:** Fixed by commit [aa4d5df](https://github.com/the-standard/smart-vault/commit/aa4d5dfb115a3962f7edcc1fa813b67c3d6950f2).

**Cyfrin:** Verified. The collateral percentage validation will no longer execute if `minted` is zero.

### Metadata

| Field | Value |
|-------|-------|
| Impact | HIGH |
| Quality Score | 0/5 |
| Rarity Score | 0/5 |
| Audit Firm | Cyfrin |
| Protocol | The Standard Auto Redemption |
| Report Date | N/A |
| Finders | Giovanni Di Siena |

### Source Links

- **Source**: https://github.com/solodit/solodit_content/blob/main/reports/Cyfrin/2024-12-18-cyfrin-the-standard-auto-redemption-v2.0.md
- **GitHub**: N/A
- **Contest**: N/A

### Keywords for Search

`vulnerability`

