---
# Core Classification
protocol: Teller Finance
chain: everychain
category: uncategorized
vulnerability_type: unknown

# Attack Vector Details
attack_type: unknown
affected_component: smart_contract

# Source Information
source: solodit
solodit_id: 32386
audit_firm: Sherlock
contest_link: https://app.sherlock.xyz/audits/contests/295
source_link: none
github_link: https://github.com/sherlock-audit/2024-04-teller-finance-judging/issues/122

# Impact Classification
severity: medium
impact: security_vulnerability
exploitability: 0.00
financial_impact: medium

# Scoring
quality_score: 0
rarity_score: 0

# Context Tags
tags:

# Audit Details
report_date: unknown
finders_count: 13
finders:
  - aman
  - kennedy1030
  - pkqs90
  - KupiaSec
  - BengalCatBalu
---

## Vulnerability Title

M-9: `LenderCommitmentGroup` pools will have incorrect exchange rate when fee-on-transfer tokens are used

### Overview


This bug report is about a problem with the `LenderCommitmentGroup` contract, which is used for loans. The issue is that when a fee-on-transfer token is used in one of the pools, the accounting for the tokens is not adjusted. This leads to an incorrect exchange rate and affects the amount of shares and tokens users receive when depositing or withdrawing. The team has been notified and a solution has been proposed. There was some discussion about whether this issue is valid, but it has been resolved and marked as a medium priority with duplicates.

### Original Finding Content

Source: https://github.com/sherlock-audit/2024-04-teller-finance-judging/issues/122 

## Found by 
0x73696d616f, 0xAnmol, 0xadrii, BengalCatBalu, BoRonGod, DPS, KupiaSec, aman, cryptic, kennedy1030, merlin, pkqs90, psb01
## Summary
`LenderCommitGroup_Smart` contract incorporates internal accounting for the amount of tokens deposited, withdrawn, etc. The problem is that if one of the pools has a fee-on-transfer token, the accounting is not adjusted. This will create inflated accountings of the tokens within the pool, and impact the exchange rate. 

## Vulnerability Detail
`LenderCommitmentGroup_Smart` is a contract that acts as it's own loan committment, which has liquidity pools with `principal token` and `collateral token`. Users can deposit `principal tokens` in exchange for `share tokens`.

Here is the flow of depositing principal tokens for shares

`LenderCommitmentGroup_Smart::addPrincipalToCommitmentGroup` []()
```javascript
    function addPrincipalToCommitmentGroup(
        uint256 _amount,
        address _sharesRecipient
    ) external returns (uint256 sharesAmount_) {
        
        // @audit if token is Fee-on-transfer, `_amount` transferred will be less
        principalToken.transferFrom(msg.sender, address(this), _amount);

@>      sharesAmount_ = _valueOfUnderlying(_amount, sharesExchangeRate());

        // @audit this will be inflated
        totalPrincipalTokensCommitted += _amount;

        // @audit Bob is minted shares dependent on original amount, not amount after transfer
        poolSharesToken.mint(_sharesRecipient, sharesAmount_);
    }
```

```javascript
    function sharesExchangeRate() public view virtual returns (uint256 rate_) {
        //@audit As more FOT tokens are deposited, this value becomes inflated
        uint256 poolTotalEstimatedValue = getPoolTotalEstimatedValue();

        // @audit EXCHANGE_RATE_EXPANSION_FACTOR = 1e36
        if (poolSharesToken.totalSupply() == 0) {
            return EXCHANGE_RATE_EXPANSION_FACTOR; // 1 to 1 for first swap
        }

        rate_ =
            (poolTotalEstimatedValue * EXCHANGE_RATE_EXPANSION_FACTOR) /
            poolSharesToken.totalSupply();
    }
```

```javascript
    function _valueOfUnderlying(
        uint256 amount,
        uint256 rate
    ) internal pure returns (uint256 value_) {
        if (rate == 0) {
            return 0;
        }

        value_ = (amount * EXCHANGE_RATE_EXPANSION_FACTOR) / rate;
    }
```

As you can see, the original `_amount` entered is used to not only issue the shares, but to keep track of the amount pool has:

```javascript
    function getPoolTotalEstimatedValue()
        public
        view
        returns (uint256 poolTotalEstimatedValue_)
    {
        // @audit This will be inflated
        int256 poolTotalEstimatedValueSigned = int256(
            totalPrincipalTokensCommitted
        ) +
            int256(totalInterestCollected) +
            int256(tokenDifferenceFromLiquidations) -
            int256(totalPrincipalTokensWithdrawn);

        poolTotalEstimatedValue_ = poolTotalEstimatedValueSigned > int256(0)
            ? uint256(poolTotalEstimatedValueSigned)
            : 0;
    }
```

If `poolTotalEstimatedValue` is inflated, then the exchange rate will be incorrect.

## Impact
As mentioned above, incorrect exchange rate calculation. Users will not receive the correct amount of shares/PT when withdrawing/depositing

## Code Snippet
https://github.com/sherlock-audit/2024-04-teller-finance/blob/main/teller-protocol-v2-audit-2024/packages/contracts/contracts/LenderCommitmentForwarder/extensions/LenderCommitmentGroup/LenderCommitmentGroup_Smart.sol#L307

## Tool used
Manual Review

## Recommendation
Check balance before and after transferring, then update accounting.



## Discussion

**sherlock-admin2**

The protocol team fixed this issue in the following PRs/commits:
https://github.com/teller-protocol/teller-protocol-v2-audit-2024/pull/37


**0xMR0**

Escalate

This is invalid issue.

The contest readme states:
> We are allowing any standard token that would be compatible with Uniswap V3 to work with our codebase, just as was the case for the original audit of TellerV2.sol . The tokens are assumed to be able to work with Uniswap V3 .

Uniswap V3 explicitely does not support Fee on transfer tokens. This can be checked [here](https://docs.uniswap.org/concepts/protocol/integration-issues#fee-on-transfer-tokens)

> Uniswap v3 does not support Fee on transfer tokens.

cc- @ethereumdegen 

**sherlock-admin3**

> Escalate
> 
> This is invalid issue.
> 
> The contest readme states:
> > We are allowing any standard token that would be compatible with Uniswap V3 to work with our codebase, just as was the case for the original audit of TellerV2.sol . The tokens are assumed to be able to work with Uniswap V3 .
> 
> Uniswap V3 explicitely does not support Fee on transfer tokens. This can be checked [here](https://docs.uniswap.org/concepts/protocol/integration-issues#fee-on-transfer-tokens)
> 
> > Uniswap v3 does not support Fee on transfer tokens.
> 
> cc- @ethereumdegen 

You've created a valid escalation!

To remove the escalation from consideration: Delete your comment.

You may delete or edit your escalation comment anytime before the 48-hour escalation window closes. After that, the escalation becomes final.

**underdog-sec**

Answering @0xMR0:
As mentioned in [the link you provided](https://docs.uniswap.org/concepts/protocol/integration-issues#fee-on-transfer-tokens): _Fee-on-transfer tokens will not function with our **router** contracts_. It is only the router which does not support fee-on-transfer tokens. All other contracts in the Uniswap v3 protocol itself **do** support fee-on-transfer tokens.

The main reasoning for the protocol behind only accepting tokens supported by Uniswap V3 is because the `LenderCommitmentGroup_Smart` requires a Uniswap pool for the expected tokens to be able to price them. This is possible with fee-on-transfer tokens as pools can be created with them, and their price can be fetched.

**0x73696d616f**

yes as @underdog-sec mentions, only the router is unsupported, pools work just fine.

**0xMR0**

> Token Integration Issues
> Fee-on-transfer and rebasing tokens will not function correctly on v3.

Tellor only intends to work with tokens that are compatible with uniswap V3 so fee on transfer tokens are not intended to be used by protocol. 

I think, @ethereumdegen can clarify it better.

**0x73696d616f**

> Tellor only intends to work with tokens that are compatible with uniswap V3 so fee on transfer tokens are not intended to be used by protocol.
I think, @ethereumdegen can clarify it better.

I don't know what clarification we need from the sponsor if it was in the readme that this intends to be compatible with Uniswap V3 and it indeed works with Uniswap V3 pools.

**nevillehuang**

@0x73696d616f @0xMR0 @underdog-sec I might have misjudged this because uniswapV2 has explicit support for FOT in their routers and I assumed the same applies. 2 questions:

1. Is there a current uniswapV3 pool that supports a FOT token?
2. Is the uniswapv3 router contract required for teller to function?

**crypticdefense**

@nevillehuang 

1. Here is an example of a V3 pool where fee-on-transfer token `PAXG` is used:  
https://etherscan.io/address/0xcb1Abb2731a48D8819f03808013C0a0E48D9B3d9#readContract
https://app.uniswap.org/explore/pools/ethereum/0xcb1Abb2731a48D8819f03808013C0a0E48D9B3d9

2. No it is not, at least not in the case of `LenderCommitmentGroup` pools. Only the `UniswapV3Pool` interface is used to fetch the pool values of tokens, ticks, etc.

**nevillehuang**

Thanks @crypticdefense, based on the above information, I believe this issue should remain valid.

**cvetanovv**

I disagree with the escalation.

The Uniswap router is incompatible with "Fee-on-transfer tokens", but this does not mean such tokens will not be used in the pool. 
The protocol is likely to use this type of tokens and should be taken into consideration.

Planning to reject the escalation and leave the issue as is.

**Evert0x**

Result:
Medium
Has Duplicates

**sherlock-admin4**

Escalations have been resolved successfully!

Escalation status:
- [0xMR0](https://github.com/sherlock-audit/2024-04-teller-finance-judging/issues/122/#issuecomment-2116550521): rejected

### Metadata

| Field | Value |
|-------|-------|
| Impact | MEDIUM |
| Quality Score | 0/5 |
| Rarity Score | 0/5 |
| Audit Firm | Sherlock |
| Protocol | Teller Finance |
| Report Date | N/A |
| Finders | aman, kennedy1030, pkqs90, KupiaSec, BengalCatBalu, 0x73696d616f, 0xadrii, cryptic, merlin, BoRonGod, 0xAnmol, psb01, DPS |

### Source Links

- **Source**: N/A
- **GitHub**: https://github.com/sherlock-audit/2024-04-teller-finance-judging/issues/122
- **Contest**: https://app.sherlock.xyz/audits/contests/295

### Keywords for Search

`vulnerability`

